plugins {
    id "fabric-loom" version "0.8-SNAPSHOT"
}

tasks.withType(JavaCompile) {
    options.release.set(17)
    options.encoding = "UTF-8"
}

configurations {
    coreLibs
}

dependencies {
    minecraft "com.mojang:minecraft:1.18.1"
    mappings "net.fabricmc:yarn:1.18.1+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.12.10"

    implementation "dev.tigr:simpleevents:1.2"

    coreLibs(project(":mesh-core")) {
        exclude module: "log4j-api"
    }

    implementation configurations.coreLibs
}

loom {
    accessWidener = file("src/main/resources/mesh.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

task standaloneJar(type: Jar) {
    from {
        tasks.remapJar.outputs.collect {
            it.files.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }
    from {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        configurations.coreLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    archiveClassifier.set("standalone")

    exclude "META-INF/maven", "META-INF/maven/**", "META-INF/versions", "META-INF/versions/**" ,
            "META-INF/proguard", "META-INF/proguard/**", "META-INF/com.android.tools",
            "META-INF/com.android.tools/**", "DebugProbesKt.bin"
}
build.dependsOn("standaloneJar")
