plugins {
    id "fabric-loom" version "0.8-SNAPSHOT"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

configurations {
    embeddedLibs
}

dependencies {
    minecraft "com.mojang:minecraft:1.18.1"
    mappings "net.fabricmc:yarn:1.18.1+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.12.10"

    implementation(project(":mesh-core"))
    embeddedLibs(project(":mesh-core"))
}

loom {
    accessWidener = file("src/main/resources/mesh.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release.set(17)
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        configurations.embeddedLibs.filter {
            it.name.startsWith("mesh-core") ||
            it.name.startsWith("reflections") ||
            it.name.startsWith("javassist") ||
            it.name.startsWith("simpleevents")
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
